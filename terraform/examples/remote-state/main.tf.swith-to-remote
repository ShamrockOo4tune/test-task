terraform {
  required_version = ">= 1.0.0, < 2.0.0"
  required_providers {
    yandex = {
      source  = "yandex-cloud/yandex"
      version = "0.77.0"
    }
    local = {
      source  = "hashicorp/local"
      version = "2.2.3"
    }
  }

  backend "s3" {
    key                         = "examples/remote_state/terraform.tfstate"
    endpoint                    = "storage.yandexcloud.net"
    region                      = "ru-central1" #"us-central-1"
    skip_region_validation      = true
    skip_credentials_validation = true
    # skip_requesting_account_id  = true        ####  fails with
    # skip_metadata_api_check     = true        ####  ValidationException: Unsupported API version ""
    # dynamodb_table              = "tf-state"  ####  status code: 400
  }
}

provider "yandex" {
  token     = var.token
  cloud_id  = var.cloud_id
  folder_id = var.folder_id
  zone      = var.zone
}

module "remote_state" {
  source      = "../../modules/remote_state"
  folder_id   = var.folder_id
  environment = "shared"
  bucket_name = var.bucket_name
  ydb_name    = var.ydb_name
}

resource "local_sensitive_file" "secret_key" {
  content         = "# This file was auto generated by terraform.\n# The file is used for moving terraform state to remote backend\n${module.remote_state.bucket_access_key_init_str}\n${module.remote_state.bucket_secret_key_init_str}\n${module.remote_state.bucket_name_init_str}\n${module.remote_state.ydb_api_endpoint}"
  filename        = "backend.auto.tfvars"
  file_permission = "0600"
}
